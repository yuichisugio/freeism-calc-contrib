# コミット作成者を評価する
jq \
  --arg task_name "create_commit_with_pr" \
  --slurpfile weighting "./src/designated-oss-growth/github-developer-contrib/archive/calc-contrib/weighting.json" \
  '
    # 最大値から、期間が過ぎた機関だけ引き算した重み付け値を返す
    # repository作成前のsponsorはmaxより評価するために、 $daysはマイナスでも0に直さない
    def clamp($max; $max_weight ;$unit; $days; $lower_limit):
      (($max * $max_weight) - ($unit * $days)) as $v
      | if $v < $lower_limit then $lower_limit else $v end;

    # 値×重み → 下限補正
    def metric_weight($src; $weight; $min):
      (($src // 0) * ($weight // 0)) as $raw
      | (if $raw < ($min // 0) then ($min // 0) else $raw end);

    def build_row_fields($repo_ts; $w):
      . as $t
      | ($t.task_date | fromdateiso8601) as $epoch_task_date
      | (((($epoch_task_date // 0) - ($repo_ts // 0)) / 86400) | floor) as $rcttp_days
      | ($w[$task_name].task_type // 0) as $tt
      | clamp(
          $w[$task_name].repo_creation_to_task_period.max_period;
          $w[$task_name].repo_creation_to_task_period.max_weight;
          $w[$task_name].repo_creation_to_task_period.minus_unit;
          $rcttp_days;
          $w[$task_name].repo_creation_to_task_period.lower_limit
        ) as $rcttp
      | metric_weight(
          $t.word_count;
          $w[$task_name].amount_of_work.word_count;
          $w[$task_name].amount_of_work.word_count_lower_limit
        ) as $aow_word
      | metric_weight(
          $t.lines_of_code;
          $w[$task_name].amount_of_work.lines_of_code;
          $w[$task_name].amount_of_work.lines_of_code_lower_limit
        ) as $aow_code
      | ( $aow_word + $aow_code ) as $aow
      | {
          "criterion_weight_for_task_type": $tt,
          "criterion_weight_for_repo_creation_to_task_period": $rcttp,
          "criterion_weight_for_amount_of_work": $aow,
          "contribution_point": ($tt * $rcttp * $aow) | round
        };

    $weighting[0] as $w
    | . as $root
    | ($root.meta.repository.created_at | fromdateiso8601) as $repo_ts

    # ユーザー配列を安全に更新（常に全体を返す）
    | .data.user = ((.data.user // []) | map(
        . as $u
        | ($u.task // []) as $tasks
        | $u + { task: ($tasks | map(
            if .task_name == $task_name then
              . + build_row_fields($repo_ts; $w)
            else .
            end
          )) }
      ))
  ' \
  "./src/designated-oss-growth/github-developer-contrib/archive/calc-contrib/integrated-processed-data.json" \
  > "./src/designated-oss-growth/github-developer-contrib/archive/calc-contrib/commit/1-1-result.json"
