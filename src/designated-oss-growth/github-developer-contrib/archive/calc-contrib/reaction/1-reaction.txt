# reactionした人を評価。
jq \
  --arg task_name "reaction" \
  --slurpfile weighting "./src/designated-oss-growth/github-developer-contrib/archive/calc-contrib/weighting.json" \
  '
    # repository作成前のsponsorはmaxより評価するために、 $daysはマイナスでも0に直さない
    def clamp($max; $unit; $days; $lower_limit):
      ($max - ($unit * $days)) as $v
      | if $v < $lower_limit then $lower_limit else $v end;

    # 値×重み → 下限補正
    def metric_weight($src; $weight; $min):
      (($src // 0) * ($weight // 0)) as $raw
      | (if $raw < $min then $min else $raw end);

    def build_row_fields($repo_ts; $w):
      . as $t
      | ($t.task_date | fromdateiso8601) as $epoch_task_date
      | (((($epoch_task_date // 0) - ($repo_ts // 0)) / 86400) | floor) as $rcttp_days
      | ($t.task_start | fromdateiso8601) as $epoch_task_start
      | (((($epoch_task_date // 0) - ($epoch_task_start // 0)) / 86400) | floor) as $rs_days
      | ($w[$task_name].task_type // 0) as $tw
      | clamp(
          $w[$task_name].repo_creation_to_task_period.max_period;
          $w[$task_name].repo_creation_to_task_period.minus_unit;
          $rcttp_days;
          $w[$task_name].repo_creation_to_task_period.lower_limit
        ) as $rcttp
      | clamp(
          $w[$task_name].response_speed.max_period;
          $w[$task_name].response_speed.minus_unit;
          $rs_days;
          $w[$task_name].response_speed.lower_limit
        ) as $rs
      | {
          "criterion_weight_for_task_type": $tw,
          "criterion_weight_for_repo_creation_to_task_period": $rcttp,
          "criterion_weight_for_response_speed": $rs,
          "contribution_point": ($tw * $rcttp * $rs) | round
        };

    $weighting[0] as $w
    | . as $root
    | ($root.meta.repository.created_at | fromdateiso8601) as $repo_ts

    # ユーザー配列を安全に更新（常に全体を返す）
    | .data.user = ((.data.user // []) | map(
        . as $u
        | ($u.task // []) as $tasks
        | $u + { task: ($tasks | map(
            if .task_name == $task_name then
              . + build_row_fields($repo_ts; $w)
            else .
            end
          )) }
      ))
  ' \
  "./src/designated-oss-growth/github-developer-contrib/archive/calc-contrib/integrated-processed-data.json" \
  > "./src/designated-oss-growth/github-developer-contrib/archive/calc-contrib/reaction/1-1-result.json"
